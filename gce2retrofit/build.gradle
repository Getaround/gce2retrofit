buildscript {
  repositories {
    jcenter()
    maven {
      def Properties properties = new Properties()
      properties.load(project.rootProject.file("local.properties").newDataInputStream())
      def artifactory_contextUrl = properties.getProperty('artifactory_contextUrl')
      url "${artifactory_contextUrl}/gradle"
      credentials {
        username = properties.getProperty('artifactory_user')
        password = properties.getProperty('artifactory_password')
      }
    }
  }
  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.5.1'
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.9.6"
  }
}

plugins {
  id 'com.gradle.plugin-publish' version '0.10.0'
  id 'java-gradle-plugin'
}


apply plugin: 'groovy'
apply plugin: 'java'

final DEVELOPMENT = false

if (!DEVELOPMENT) {
  apply plugin: 'nexus'
  apply plugin: "com.jfrog.artifactory"
  apply plugin: 'maven-publish'
}

repositories {
  jcenter()
}

dependencies {
  implementation gradleApi()
  implementation localGroovy()

  implementation 'com.android.tools.build:gradle:2.1.0'

  implementation 'com.squareup.retrofit:retrofit:1.9.0'
  implementation 'com.google.code.gson:gson:2.4'
  implementation 'com.squareup:javawriter:2.5.1'
  implementation 'org.apache.commons:commons-lang3:3.3.2'
  implementation 'commons-cli:commons-cli:1.2'

  testImplementation 'junit:junit:4.12'
  testImplementation 'com.google.truth:truth:0.25'
}

jar {
  doFirst {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  }
  exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
  manifest.attributes("Main-Class": "com.sqisland.gce2retrofit.Generator")
  archiveName "gce2retrofit.jar"
}

apply plugin: 'maven'

group = 'com.sqisland'
version = '2.0.0-SNAPSHOT'

install {
  repositories.mavenInstaller {
    pom.artifactId = 'gce2retrofit'
  }
}
uploadArchives {
  repositories.mavenDeployer {
    pom.artifactId = 'gce2retrofit'
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
  }
}

if (!DEVELOPMENT) {
  modifyPom {
    project {
      name 'gce2retrofit'
      description "Gradle plugin to generate Retrofit classes from Google Cloud Endpoint (GCE) discovery file"
      url 'https://github.com/chiuki/gce2retrofit'
      inceptionYear '2015'

      scm {
        url 'https://github.com/chiuki/gce2retrofit'
        connection 'scm:git:git://github.com/chiuki/gce2retrofit.git'
        developerConnection 'scm:git:ssh://git@github.com/chiuki/gce2retrofit.git'
      }

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'chiuki'
          name 'Chiu-Ki Chan'
          email 'chiuki@sqisland.com'
        }
      }
    }
  }
}

if (!DEVELOPMENT) {
  Properties project_properties = new Properties()
  project_properties.load(project.rootProject.file("local.properties").newDataInputStream())

  def artifactory_user = project_properties.getProperty('artifactory_user')
  def artifactory_password = project_properties.getProperty('artifactory_password')
  def artifactory_contextUrl = project_properties.getProperty('artifactory_contextUrl')


  artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
      repository {
        repoKey = 'gradle-local'
        username = "${artifactory_user}"
        password = "${artifactory_password}"
        maven = true
      }
      defaults {
        publications('mavenJava')
        properties = [
            'published.by': System.properties['user.name']
        ]
        publishArtifacts = true
        publishPom = true
      }
    }
    resolve {
      repository {
        repoKey = 'gradle'
        username = "${artifactory_user}"
        password = "${artifactory_password}"
        maven = true

      }
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        groupId group
        version = project.version
        artifactId 'gce2retrofit'
      }
    }
  }
}
